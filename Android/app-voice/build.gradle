plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    compileSdk 32

    defaultConfig {
        applicationId "io.agora.voicechat.uikit"
        minSdk 24
        targetSdk 32
        versionCode 3
        versionName "1.0.1"

        def properties = new Properties()
        properties.load(rootProject.file("local.properties").newDataInputStream())

        final def SERVER_HOST = properties.getProperty("SERVER_HOST") ?: ""
        final def AGORA_APP_ID = properties.getProperty("AGORA_APP_ID") ?: ""
        final def AGORA_APP_CERT = properties.getProperty("AGORA_APP_CERT") ?: ""
        final def AGORA_BASIC_AUTH = properties.getProperty("AGORA_BASIC_AUTH") ?: ""
        final def IM_APP_KEY = properties.getProperty("IM_APP_KEY") ?: ""
        final def IM_CLIENT_ID = properties.getProperty("IM_CLIENT_ID") ?: ""
        final def IM_CLIENT_SECRET = properties.getProperty("IM_CLIENT_SECRET") ?: ""
        if (SERVER_HOST.isEmpty()) {
            throw new GradleException("请在 local.properties 中配置完整的 SERVER_HOST")
        }
        if (AGORA_APP_ID.isEmpty()) {
            throw new GradleException("请在 local.properties 中配置完整的 AGORA_APP_ID")
        }

        buildConfigField "String", "SERVER_HOST", "\"$SERVER_HOST\""
        buildConfigField "String", "AGORA_APP_ID", "\"$AGORA_APP_ID\""
        buildConfigField "String", "AGORA_APP_CERT", "\"$AGORA_APP_CERT\""
        buildConfigField "String", "AGORA_BASIC_AUTH", "\"$AGORA_BASIC_AUTH\""
        buildConfigField "String", "IM_APP_KEY", "\"$IM_APP_KEY\""
        buildConfigField "String", "IM_CLIENT_ID", "\"$IM_CLIENT_ID\""
        buildConfigField "String", "IM_CLIENT_SECRET", "\"$IM_CLIENT_SECRET\""
        ndk {
            abiFilters 'arm64-v8a', 'armeabi-v7a'
        }
        //用于设置使用as打包so时指定输出目录
        externalNativeBuild {
            ndkBuild {
                abiFilters  "arm64-v8a","armeabi-v7a"
            }
        }
    }

    signingConfigs {
        debug {
            storeFile file('../keystore')
            storePassword '230713'
            keyAlias 'voiceroom'
            keyPassword '230713'
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    buildFeatures {
        viewBinding true
    }

    sourceSets {
        main {
            res.srcDirs += "src/main/java/io/agora/app/voice/kit/res"
        }
    }

    applicationVariants.all {
        variant ->
            variant.outputs.all { output ->
                outputFileName = new File(rootProject.name + "_" +
                        "v" + defaultConfig.versionName + "_" +
                        new Date().format("yyyyMMddHHmm") + "_" +
                        buildType.name + ".apk")
            }
    }

    packagingOptions {
        //排除非必须的插件
        exclude 'lib/arm64-v8a/libagora_*_extension.so'
        exclude 'lib/armeabi-v7a/libagora_*_extension.so'
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'

    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'com.github.bumptech.glide:glide:4.15.0'
    implementation project(':asceneskit')
}